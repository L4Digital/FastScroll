ext['ossrhUsername'] = ''
ext['ossrhPassword'] = ''
ext['sonatypeStagingProfileId'] = ''
ext['signing.keyId'] = ''
ext['signing.password'] = ''
ext['signing.secretKeyFile'] = ''

File secretPropsFile = rootProject.file('local.properties')

if (secretPropsFile.exists()) {
    // Read local.properties file first if it exists
    Properties props = new Properties()
    new FileInputStream(secretPropsFile).withCloseable { is -> props.load(is) }
    props.each { name, value -> ext[name] = value }
} else {
    // Use system environment variables
    ext['ossrhUsername'] = System.getenv('OSSRH_USERNAME')
    ext['ossrhPassword'] = System.getenv('OSSRH_PASSWORD')
    ext['sonatypeStagingProfileId'] = System.getenv('STAGING_PROFILE_ID')
    ext['signing.keyId'] = System.getenv('SIGNING_KEY_ID')
    ext['signing.password'] = System.getenv('SIGNING_PASSWORD')
    ext['signing.secretKeyFile'] = System.getenv('SIGNING_SECRET_KEY_FILE')
}

// Set up Sonatype repository
nexusPublishing {
    repositories {
        sonatype {
            stagingProfileId = sonatypeStagingProfileId
            username = ossrhUsername
            password = ossrhPassword
            nexusUrl.set(uri('https://s01.oss.sonatype.org/service/local/'))
            snapshotRepositoryUrl.set(uri('https://s01.oss.sonatype.org/content/repositories/snapshots/'))
        }
    }
}
